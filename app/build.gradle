def config = configHelper.fetchConfig()
def appId = config.getOrDefault("APPLICATION_ID", "org.openedx.app")
def themeDirectory = config.getOrDefault("THEME_DIRECTORY", "openedx")
def firebaseConfig = config.get('FIREBASE')
def firebaseEnabled = firebaseConfig?.getOrDefault('ENABLED', false)

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

if (firebaseEnabled) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    tasks.register('generateGoogleServicesJson') {
        configHelper.generateGoogleServicesJson(appId)
    }

    preBuild.dependsOn(generateGoogleServicesJson)
} else {
    tasks.register('removeGoogleServicesJson') {
        configHelper.removeGoogleServicesJson()
    }

    preBuild.dependsOn(removeGoogleServicesJson)
}

android {
    namespace 'org.openedx.app'
    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId appId
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode 1
        versionName "1.0.0"

        resourceConfigurations += ["en", "uk"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    flavorDimensions += "env"
    productFlavors {
        prod {
            dimension 'env'
            setupBranchConfigFields(it)
            setupFirebaseConfigFields(it)
        }
        develop {
            dimension 'env'
            setupBranchConfigFields(it)
            setupFirebaseConfigFields(it)
        }
        stage {
            dimension 'env'
            setupBranchConfigFields(it)
            setupFirebaseConfigFields(it)
        }
    }

    sourceSets {
        prod {
            res.srcDirs = ["src/$themeDirectory/res"]
        }
        develop {
            res.srcDirs = ["src/$themeDirectory/res"]
        }
        stage {
            res.srcDirs = ["src/$themeDirectory/res"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (firebaseEnabled) {
                firebaseCrashlytics {
                    mappingFileUploadEnabled false
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = java_version
        freeCompilerArgs = List.of("-Xstring-concat=inline")
    }
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation project(path: ':core')
    implementation project(path: ':auth')
    implementation project(path: ':course')
    implementation project(path: ':dashboard')
    implementation project(path: ':discovery')
    implementation project(path: ':profile')
    implementation project(path: ':discussion')
    implementation project(path: ':whatsnew')
    implementation project(path: ':downloads')

    ksp "androidx.room:room-compiler:$room_version"

    implementation "androidx.core:core-splashscreen:$core_splashscreen_version"

    api platform("com.google.firebase:firebase-bom:$firebase_version")
    api "com.google.firebase:firebase-messaging"

    // Braze SDK Integration
    implementation "com.braze:android-sdk-ui:$braze_sdk_version"

    // Plugins
    implementation("com.github.openedx:openedx-app-firebase-analytics-android:$openedx_firebase_analytics_version")

    androidTestImplementation "androidx.test.ext:junit:$test_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.mockk:mockk-android:$mockk_version"
    testImplementation "androidx.arch.core:core-testing:$android_arch_version"
}

private def setupBranchConfigFields(buildType) {
    def branchConfig = configHelper.fetchConfig().get("BRANCH")
    def branchKey = ""
    def branchUriScheme = ""
    def branchHost = ""
    def branchAlternateHost = ""

    if (branchConfig && branchConfig.get("ENABLED")) {
        branchKey = branchConfig.getOrDefault("KEY", "")
        branchUriScheme = branchConfig.getOrDefault("URI_SCHEME", "")
        branchHost = branchConfig.getOrDefault("HOST", "")
        branchAlternateHost = branchConfig.getOrDefault("ALTERNATE_HOST", "")

        // Validation: Throw exception if any field is empty
        if (branchKey.isEmpty() || branchUriScheme.isEmpty() || branchHost.isEmpty() ||
                branchAlternateHost.isEmpty()) {
            throw new IllegalStateException("One or more Branch configuration fields are empty.")
        }
    }

    buildType.resValue "string", "branch_key", branchKey
    buildType.resValue "string", "branch_uri_scheme", branchUriScheme
    buildType.resValue "string", "branch_host", branchHost
    buildType.resValue "string", "branch_alternate_host", branchAlternateHost
}

private def setupFirebaseConfigFields(buildType) {
    def firebaseConfig = configHelper.fetchConfig().get('FIREBASE')
    def firebaseEnabled = firebaseConfig?.getOrDefault('ENABLED', false)
    def cloudMessagingEnabled = firebaseConfig?.getOrDefault('CLOUD_MESSAGING_ENABLED', false)

    buildType.manifestPlaceholders = [fcmEnabled: firebaseEnabled && cloudMessagingEnabled]
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}
