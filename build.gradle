import io.gitlab.arturbosch.detekt.Detekt
import org.edx.builder.ConfigHelper
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

import java.util.regex.Matcher
import java.util.regex.Pattern

buildscript {
    ext {
        // Plugin versions
        android_gradle_plugin_version = '8.12.2'
        google_services_version = '4.4.3'
        firebase_crashlytics_version = '3.0.6'
        ksp_version = '2.2.10-2.0.2'

        //Depends on versions in OEXFoundation
        kotlin_version = '2.2.10'
        room_version = '2.7.2'
        detekt_version = '1.23.8'

        // Library versions
        media3_version = "1.8.0"
        youtubeplayer_version = "11.1.0"
        firebase_version = "33.0.0"
        jsoup_version = '1.21.2'
        in_app_review = '2.0.2'
        extented_spans_version = "1.4.0"
        zip_version = '2.11.5'

        // Third-party library versions
        branch_sdk_version = '5.20.0'
        play_services_ads_identifier_version = '18.2.0'
        install_referrer_version = '2.2'
        snakeyaml_version = '2.4'
        openedx_foundation_version = '1.0.2'
        openedx_firebase_analytics_version = '1.0.1'
        braze_sdk_version = '37.0.0'

        // AndroidX library versions
        core_splashscreen_version = '1.0.1'
        activity_compose_version = '1.10.1'
        browser_version = '1.9.0'
        credentials_version = '1.5.0'

        // Social login versions
        facebook_login_version = '18.1.3'
        play_services_auth_version = '21.4.0'
        googleid_version = '1.1.1'
        msal_version = '7.0.0'

        // OpenTelemetry versions
        opentelemetry_version = '1.53.0'

        // Testing versions
        compose_ui_tooling = '1.7.8'
        mockk_version = '1.14.5'
        android_arch_version = '2.2.0'
        junit_version = '4.13.2'
        test_ext_version = '1.3.0'
        espresso_version = '3.7.0'
        kotlinx_coroutines_test_version = '1.10.2'
    }
}

plugins {
    //noinspection GradlePluginVersion
    id 'com.android.application' version "$android_gradle_plugin_version" apply false
    //noinspection GradlePluginVersion
    id 'com.android.library' version "$android_gradle_plugin_version" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.gms.google-services' version "$google_services_version" apply false
    id "com.google.firebase.crashlytics" version "$firebase_crashlytics_version" apply false
    id "com.google.devtools.ksp" version "$ksp_version" apply false
    id "org.jetbrains.kotlin.plugin.compose" version "$kotlin_version" apply false
    id 'io.gitlab.arturbosch.detekt' version "$detekt_version" apply false
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

ext {
    // Android SDK versions
    compile_sdk_version = 36
    target_sdk_version = 36
    min_sdk_version = 24
    java_version = JavaVersion.VERSION_17
    jvm_target_version = JvmTarget.JVM_17

    configHelper = new ConfigHelper(projectDir, getCurrentFlavor())
}

def getCurrentFlavor() {
    String tskReqStr = getGradle().getStartParameter().getTaskRequests().toString()
    Pattern pattern
    if (tskReqStr.contains("assemble")) // to run ./gradlew assembleRelease to build APK
        pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
    else if (tskReqStr.contains("bundle")) // to run ./gradlew bundleRelease to build .aab
        pattern = Pattern.compile("bundle(\\w+)(Release|Debug)")
    else
        pattern = Pattern.compile("generate(\\w+)(Release|Debug)")
    Matcher matcher = pattern.matcher(tskReqStr)
    if (matcher.find()) {
        return matcher.group(1).toLowerCase()
    } else {
        return ""
    }
}

tasks.register('generateMockedRawFile') {
    doLast { configHelper.generateMicrosoftConfig() }
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt.yml")
def basePathFile = rootProject.projectDir.absolutePath
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

apply plugin: 'io.gitlab.arturbosch.detekt'

tasks.register("detektAll", Detekt) {
    def autoFix = project.hasProperty('detektAutoFix')

    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = autoFix
    buildUponDefaultConfig = true
    setSource(projectSource)
    config.setFrom(configFile)
    include(kotlinFiles)
    basePath(basePathFile)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled(true)
        xml.enabled(true)
        txt.enabled(false)
        sarif.enabled(true)
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
}
