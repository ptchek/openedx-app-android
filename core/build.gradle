buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.yaml:snakeyaml:$snakeyaml_version"
    }
}

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id "org.jetbrains.kotlin.plugin.compose"
}

def currentFlavour = getCurrentFlavor()
def config = configHelper.fetchConfig()
def themeDirectory = config.getOrDefault("THEME_DIRECTORY", "openedx")

android {
    namespace 'org.openedx.core'
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    flavorDimensions += "env"
    productFlavors {
        prod {
            dimension 'env'
            insertBuildConfigFields(currentFlavour, it)
        }
        develop {
            dimension 'env'
            insertBuildConfigFields(currentFlavour, it)
        }
        stage {
            dimension 'env'
            insertBuildConfigFields(currentFlavour, it)
        }
    }

    sourceSets {
        prod {
            java.srcDirs = ["src/$themeDirectory"]
            res.srcDirs = ["src/$themeDirectory/res"]
        }
        develop {
            java.srcDirs = ["src/$themeDirectory"]
            res.srcDirs = ["src/$themeDirectory/res"]
        }
        stage {
            java.srcDirs = ["src/$themeDirectory"]
            res.srcDirs = ["src/$themeDirectory/res"]
        }
        main {
            assets {
                srcDirs 'src/main/assets', 'assets'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlin {
        compilerOptions {
            jvmTarget = jvm_target_version
            freeCompilerArgs = ['-XXLanguage:+PropertyParamAnnotationDefaultTargetMode']
        }
    }
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    // Room
    ksp "androidx.room:room-compiler:$room_version"

    // jsoup
    api "org.jsoup:jsoup:$jsoup_version"

    // Firebase
    api platform("com.google.firebase:firebase-bom:$firebase_version")
    api 'com.google.firebase:firebase-common-ktx'
    api "com.google.firebase:firebase-crashlytics-ktx"

    //Play In-App Review
    api "com.google.android.play:review-ktx:$in_app_review"

    // Branch SDK Integration
    api "io.branch.sdk.android:library:$branch_sdk_version"
    api "com.google.android.gms:play-services-ads-identifier:$play_services_ads_identifier_version"
    api "com.android.installreferrer:installreferrer:$install_referrer_version"

    // Zip
    api "net.lingala.zip4j:zip4j:$zip_version"

    //  OpenEdx libs
    api("com.github.openedx:openedx-app-foundation-android:$openedx_foundation_version")

    // Preview
    debugApi "androidx.compose.ui:ui-tooling:$compose_ui_tooling"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}

def insertBuildConfigFields(currentFlavour, buildType) {
    if (currentFlavour == buildType.name) {
        configHelper.generateConfigJson()
        configHelper.generateMicrosoftConfig()
    }
    def config = configHelper.fetchConfig()
    def platformName = config.getOrDefault("PLATFORM_NAME", "")
    def platformFullName = config.getOrDefault("PLATFORM_FULL_NAME", "")

    buildType.resValue "string", "platform_name", platformName
    buildType.resValue "string", "platform_full_name", platformFullName
    insertFacebookBuildConfigFields(config, buildType)
    insertMicrosoftManifestPlaceholder(config, buildType)
}

static def insertFacebookBuildConfigFields(config, buildType) {
    def facebook = config.get("FACEBOOK")
    def facebookAppId = ""
    def facebookClientToken = ""
    if (facebook) {
        facebookAppId = facebook.getOrDefault("FACEBOOK_APP_ID", "")
        facebookClientToken = facebook.getOrDefault("CLIENT_TOKEN", "")
    }
    buildType.resValue "string", "facebook_app_id", facebookAppId
    buildType.resValue "string", "fb_login_protocol_scheme", "fb$facebookAppId"
    buildType.resValue "string", "facebook_client_token", facebookClientToken
}

static def insertMicrosoftManifestPlaceholder(config, buildType) {
    def microsoft = config.get("MICROSOFT")
    def microsoftPackageSignature = ""
    if (microsoft) {
        microsoftPackageSignature = microsoft.getOrDefault("PACKAGE_SIGNATURE", "")
    }
    buildType.manifestPlaceholders = [microsoftSignature: microsoftPackageSignature]
}
